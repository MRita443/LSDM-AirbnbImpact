PREFIX : <http://example.org/nyc-vkg#>

SELECT ?borough
       (COALESCE(?suspiciousListingsCount, 0) AS ?suspiciousListings)
       (COALESCE(?familyHouseSalesCount, 0) AS ?familyHouseSales)
WHERE {

  OPTIONAL {
    SELECT ?borough (COUNT(DISTINCT ?listing) AS ?suspiciousListingsCount)
    WHERE {
      ?listing a :AirbnbListing ;
               :room_type "Entire home/apt" ;
               :availability_year ?avail ;
               :locatedIn ?neighborhood ;
               :owns ?host .

      ?host :num_local_listings ?num_local_listings .
      ?neighborhood :partOf ?borough .

      FILTER(?avail > 300 || ?num_local_listings > 2)
    }
    GROUP BY ?borough
  }

  OPTIONAL {
    SELECT ?borough (COUNT(DISTINCT ?sale) AS ?familyHouseSalesCount)
    WHERE {
      ?sale a :PropertySale ;
            :building_category ?cat ;
            :locatedIn ?neighborhood .

      ?neighborhood :partOf ?borough .

      FILTER(
        ?cat = "01 ONE FAMILY DWELLINGS" ||
        ?cat = "02 TWO FAMILY DWELLINGS"
      )
    }
    GROUP BY ?borough
  }
}
ORDER BY DESC(?suspiciousListings) DESC(?familyHouseSales)