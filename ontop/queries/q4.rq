PREFIX : <http://example.org/nyc-vkg#>

SELECT ?boroughName
       (SUM(?suspiciousCount) AS ?totalSuspicious)
       (SUM(?familySalesCount) AS ?totalFamilySales)
WHERE {
  ?borough a :Borough ;
           :name ?boroughName .
  ?neighborhood :partOf ?borough .

  {
    SELECT ?neighborhood (COUNT(DISTINCT ?listing) AS ?suspiciousCount)
    WHERE {
      ?listing a :AirbnbListing ;
                 :room_type "Entire home/apt" ;
                 :locatedIn ?neighborhood ;
                 :availability_year ?avail ;
                 :owns ?host .
      ?host :num_local_listings ?num_local_listings .
      FILTER(?avail > 300 || ?num_local_listings > 2)
    }
    GROUP BY ?neighborhood
  }

  UNION 
  {
    SELECT ?neighborhood (COUNT(DISTINCT ?sale) AS ?familySalesCount)
    WHERE {
      ?sale a :PropertySale ;
            :building_category ?cat ;
            :locatedIn ?neighborhood .
      # Using VALUES is often faster than FILTER with OR strings
      VALUES ?cat { "01 ONE FAMILY DWELLINGS" "02 TWO FAMILY DWELLINGS" }
    }
    GROUP BY ?neighborhood
  }
}
GROUP BY ?boroughName
ORDER BY DESC(?totalSuspicious) DESC(?totalFamilySales)

